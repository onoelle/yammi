dnl http://www.vandenoever.info/software/KDEautomakemanual.html
dnl http://www.geekcomix.com/snh/files/docs/sdl-kdev/sdl-kdev-mini-how2-2.html
dnl http://ac-archive.sourceforge.net/acinclude.html


dnl Original Author was Kalle@kde.org
dnl I lifted it in some mater. (Stephan Kulow)
dnl I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.
dnl tested with autoconf --version
dnl tested (GNU Autoconf) 2.53

dnl tested with automake --version
dnl automake (GNU automake) 1.6.3

dnl autoconf >=2.54 and automake 1.7.1 are incompatible with yammi's
dnl build infrastructure.
dnl Search for AC_Dest on google, to
dnl see that this is a known problem with kdevelop projects
dnl Someone using very recent versions of
dnl the GNU auto tools will not be able to recreate e.g.
dnl config.h.in
dnl Autoconf 2.53 and automake 1.6.3 will work. 

dnl http://mail.gnome.org/archives/gnome-i18n/2001-August/msg00037.html
dnl autoconf-2.54 is supposedly buggy
dnl If you get an AC_Dest error then upgrade autoconf
dnl It was caused by some 'dnl ...' lines inside
dnl the AC_OUTPUT macro ??.


AC_INIT(acinclude.m4) dnl a source file from your sub dir

dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)

AC_CANONICAL_SYSTEM 

dnl Perform program name transformation
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(yammi,1.0-rc1)

dnl for non KDE applications
AC_PREFIX_DEFAULT(/usr/local)
if test "x$prefix" = "xNONE"; then
  prefix=$ac_default_prefix
  ac_configure_args="$ac_configure_args --prefix $prefix"
fi

dnl generate a header that will have all the
dnl #defines for conditional compilation
AM_CONFIG_HEADER( [config.h] )

dnl #define's that we want autoheader to create for us
dnl used to be a template in acconfig.h but now they're all here

dnl hopefully avoid some problems
AC_CHECK_FILE( [acconfig.h], AC_MSG_ERROR( [delete acconfig.h] ), )
AC_CHECK_FILE( [config.h.in], , AC_MSG_ERROR( [run autoheader] ) )

dnl defined by AM_INIT_AUTOMAKE
AH_TEMPLATE( [PACKAGE], [yammi package] )
AH_TEMPLATE( [VERSION], [yammi version] )

AH_TEMPLATE( [HAVE_LIBSM], [libsm] )
AH_TEMPLATE( [HAVE_CATGETS], [catgets] )
AH_TEMPLATE( [HAVE_GETTEXT], [gettext] )
AH_TEMPLATE( [HAVE_LC_MESSAGES], [lc_messages] )
AH_TEMPLATE( [HAVE_STRCPY], [strcpy] )
AH_TEMPLATE( [ENABLE_NLS], [nls] )

AH_TEMPLATE( [HAVE_ICONV_OPEN], [iconv_open] )
AH_TEMPLATE( [HAVE_LANGINFO_CODESET], [langinfo_codeset] )

AH_TEMPLATE( [ENABLE_ID3LIB], [Support reading and writing MP3 tags] )
AH_TEMPLATE( [ENABLE_OGGLIBS], [Support reading and writing OGG tags] )
AH_TEMPLATE( [ENABLE_XMMS], [Support xmms as media player] )
AH_TEMPLATE( [ENABLE_NOATUN], [Support noatun as media player] )

dnl -------------------------------
dnl Configure switches.
dnl -------------------------------
AC_ARG_ENABLE( [KDE],
               AC_HELP_STRING( [--disable-KDE],
                               [disable support for KDE] ),
               configure_enable_KDE=$enableval,
               configure_enable_KDE=yes )

AC_ARG_ENABLE( [xmms],
               AC_HELP_STRING( [--disable-xmms],
                               [disable support for xmms (don't need xmmsctrl)] ),
               configure_enable_xmms=$enableval,
               configure_enable_xmms=yes )

AC_ARG_ENABLE( [noatun],
             AC_HELP_STRING( [--disable-noatun],
                             [disable support for noatun (don't use kdelibs)] ),
             configure_enable_noatun=$enableval,
             configure_enable_noatun=yes )

AC_ARG_ENABLE( [ogglibs],
             AC_HELP_STRING( [--disable-ogglibs],
                             [disable support of ogglibs for reading/writing ogg tags] ),
             configure_enable_ogglibs=$enableval,
             configure_enable_ogglibs=yes )

AC_ARG_ENABLE( [id3lib],
               AC_HELP_STRING( [--disable-id3lib],
                               [disable support of id3lib for reading id3 tags] ),
               configure_enable_id3lib=$enableval,
               configure_enable_id3lib=yes )

AC_MSG_NOTICE( [
 Requested:
 KDE environment.......: $configure_enable_KDE
 XMMS support .........: $configure_enable_xmms
 Noatun support .......: $configure_enable_noatun
 ogglibs support ......: $configure_enable_ogglibs
 id3lib support .......: $configure_enable_id3lib
] )

dnl for KDE applications
if test "x$configure_enable_KDE" = "xyes"; then
    KDE_SET_PREFIX
    KDE_USE_QT(3)
fi

dnl Checks for programs.
AC_CHECK_COMPILERS

dnl require a C++ compiler
AC_LANG(C++)
AC_REQUIRE_CPP

AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)

dnl change to KDE_PROG_LIBTOOL which is just a wrapper,
dnl see "acinclude.m4" for the definition of that function
AC_PROG_LIBTOOL

dnl http://autoqt.sourceforge.net/
dnl gw_CHECK_QT

dnl http://www.gnu.org/manual/autoconf/html_mono/autoconf.html#SEC83
dnl substitution of variables in Makefiles

dnl all_libraries="$all_libraries $USER_LDFLAGS"
dnl all_includes="$all_includes $USER_INCLUDES"
dnl AC_SUBST(all_includes)
dnl AC_SUBST(all_libraries)

if test "x$configure_enable_KDE" = "xyes"; then
    AC_MSG_NOTICE( [Adding KDE support] )

    dnl for NLS support. Call them in this order!
    dnl WITH_NLS is for the po files
    AM_KDE_WITH_NLS
fi

dnl test for QT
AC_PATH_KDE

if test "x$configure_enable_KDE" = "xyes"; then
    KDE_CREATE_SUBDIRSLIST
fi

dnl -------------------------------
dnl Checks for libraries.
dnl -------------------------------

dnl standard C++ library
AC_CHECK_LIB( stdc++, main, , AC_MSG_ERROR( [*** stdc++ library is required ***] ), )

dnl OGG vorbis libraries
if test "x$configure_enable_ogglibs" = "xyes"; then
    dnl Libraries required for reading ogg tags,
    dnl if not found 'configure_enable_ogglibs' is disabled

    AC_CHECK_LIB(ogg, ogg_sync_init, , configure_enable_ogglibs=no, )
    if test "x$configure_enable_ogglibs" = "xyes"; then
        AC_CHECK_LIB( vorbis, vorbis_comment_init,, configure_enable_ogglibs=no, )
    fi

    if test "x$configure_enable_ogglibs" = "xyes"; then
        AC_CHECK_LIB( vorbisfile, ov_open,, configure_enable_ogglibs=no, )
    fi

    if test "x$configure_enable_id3lib" = "xyes"; then
        AC_CHECK_LIB( gdk, g_free,, configure_enable_ogglibs=no, )
    fi
fi

if test "x$configure_enable_ogglibs" = "xyes"; then
    AC_MSG_NOTICE( reading/writing of tags in ogg files is supported )
    AC_DEFINE( [ENABLE_OGGLIBS], [1] )
else
    AC_MSG_WARN( [*** Warning: ogg libs support disabled => no reading/writing of tags in ogg files! ***] )
fi

dnl id3lib library
if test "x$configure_enable_id3lib" = "xyes"; then
    AC_CHECK_LIB( z, deflate,, configure_enable_id3lib=no, )

    dnl if still adding libid3 then...
    if test "x$configure_enable_id3lib" = "xyes"; then
        AC_CHECK_LIB( id3, ID3Tag_Link,, configure_enable_id3lib=no, )
    fi
fi

if test "x$configure_enable_id3lib" = "xyes"; then
    AC_MSG_NOTICE( [reading/writing of tags in MP3 files is supported] )
    AC_DEFINE( [ENABLE_ID3LIB], [1] )
else
    AC_MSG_WARN( [*** Warning: id3lib support disabled => no reading/writing of tags in mp3 files! ***] )
fi


dnl XMMS support (new version)
dnl AM_PATH_XMMS([MINIMUM-VERSION, [ACTION-IF-FOUND [, ACTION-IF-NOT-FOUND]]])
if test "x$configure_enable_xmms" = "xyes"; then
    AM_PATH_XMMS(1.2.6, ,AC_MSG_ERROR([*** XMMS >= 1.2.6 not found - please install it or run configure script with --disable-xmms ***] ))
    LIBS="$XMMS_LIBS $LIBS"
    CFLAGS="$XMMS_CFLAGS $CFLAGS"
    AC_DEFINE( [ENABLE_XMMS], [1] )
fi


dnl Noatun support (for proper kde app: add "-lkdeui -lkio ")
dnl check to see if KDE->DCOP is available
if test "x$configure_enable_noatun" = "xyes"; then
    dnl additional path to libraries for linking KDE
    OLDLDFLAGS=$LDFLAGS
    LDFLAGS="$LDFLAGS -L$KDEDIR/libkdecore"
    CPPFLAGS="$CPPFLAGS -I$KDEDIR/include"

    AC_CHECK_LIB( kdecore, main, , configure_enable_noatun=no , )

    dnl    if test "x$configure_enable_noatun" = "xyes"; then
    dnl        AC_CHECK_LIB( DCOP, main, , configure_enable_noatun=no , )
    dnl    fi
    LDFLAGS=$OLDLDFLAGS
fi

if test "x$configure_enable_noatun" = "xyes"; then
    AC_MSG_NOTICE( [*** noatun support enabled ***] )
    AC_DEFINE( [ENABLE_NOATUN], [1] )
else
    AC_MSG_WARN( [*** noatun support disabled ***] )
fi

dnl ---------------------
dnl end of library checks
dnl ---------------------

if [test "x$configure_enable_xmms" != "xyes"] && [test "x$configure_enable_noatun" != "xyes"] ; then
    AC_MSG_WARN( [***BOTH xmms and noatun support are disabled, another media player with yammi plugins will be needed. ***] )
fi

dnl add here all your Makefiles. These are created by configure
dnl from Makefile.in which is create by automake from Makefile.am

dnl to work with newer autoconfs AC_OUTPUT with arguments has been changed
dnl to the following
dnl http://lists.arabeyes.org/archives/developer/2003/January/msg00099.html
AC_CONFIG_FILES(Makefile)
AC_CONFIG_FILES(yammi/Makefile)
AC_CONFIG_FILES(yammi/docs/Makefile)
AC_CONFIG_FILES(yammi/docs/en/Makefile)
AC_CONFIG_FILES(yammi/icons/Makefile)
AC_OUTPUT

AC_MSG_NOTICE( [
Configuration for $PACKAGE $VERSION :
--------------------------------
 Source code location .: $srcdir
 Host System Type .....: $host
 Preprocessor .........: $CC $CPPFLAGS
 Compiler .............: $CC $CFLAGS $LIBFLAC_CFLAGS
 Linker ...............: $CC $LDFLAGS $LIBS $LIBFLAC_LIBS
 KDE environment.......: $configure_enable_KDE
 XMMS support .........: $configure_enable_xmms
 Noatun support .......: $configure_enable_noatun
 ogglibs support ......: $configure_enable_ogglibs
 id3lib support .......: $configure_enable_id3lib
 Install path .........: $prefix
   If this path does not exist or is not the desired install path,
   you should re-run configure, giving a suitable path with the --prefix option
   For example ./configure --prefix=/opt/kde3

 Look for any WARNINGs output by the configure script (run ./configure --help to see all options).
 If you're satisfied, type 'make' to build $PACKAGE $VERSION,
 and then 'make install' for installation.
] )
